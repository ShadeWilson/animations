class Snow {
  float x = random(width);
  float y = random(-500, -50);
  float z = random(0, 1);
  float thick = map(z, 0, 1, 0.5, 4);
  boolean hasFallen = false;
  
  float yspeedMax = 1;
  float yspeedMin = 0.3;
  
  float xspeedMax = 0.05 * z;
  float xspeedLimit = 0.15 * z;
  float xaccelMax = 0.01 * z;
  
  float yspeed = map(z, 0, 1, yspeedMin, yspeedMax);
  float xspeed = random(-xspeedMax, xspeedMax);
  float xaccel = random(-xaccelMax, xaccelMax);
  
  void fall() {
    
    xspeed = xspeed + xaccel;
    x = x + xspeed;
    y = y + yspeed;
    
    
    // if snowflakes reach the bottom of the screen, or
    // if snowflakes go off the screen left or right, or
    // if the snowflake has contacted a fallen snowflake
    // they're reset at the top
    if (y >= height + 1 || x > width || x < 0 || hasFallen == true) {
      reset();
    }
    
    // if the speed in the x direction is greater than the limit
    // the acceleration is switched to the other direction
    // with a random scalar from 0 - xaccelMax
    if (xspeed > xspeedLimit || xspeed < -xspeedLimit) {
      if (xaccel > 0) {
        xaccel = random(-xaccelMax);
      } else {
        xaccel = random(xaccelMax);
      }
    }
  }
  
  void reset() {
    y = random(-200, -100);
      yspeed = map(z, 0, 1, yspeedMin, yspeedMax);
      x = random(width);
      xspeed = random(-xspeedMax, xspeedMax);
      hasFallen = false;
  }
  
  
  void show() {
    noStroke();
    fill(255, 255, 255);
    ellipse(x, y, thick, thick);
  }

  
  
}